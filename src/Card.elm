module Card exposing (Card, Deck, Face(..), Suit(..), compareCards, newDeck, shuffleDeck, viewCard, viewCardBack)

import Element exposing (Color, Element)
import Element.Background
import Element.Font
import Element.Region
import Html.Attributes
import Random exposing (Generator)
import Random.List


type alias Deck =
    List Card


allFaces : List Face
allFaces =
    [ Ace
    , Two
    , Three
    , Four
    , Five
    , Six
    , Seven
    , Eight
    , Nine
    , Ten
    , Jack
    , Queen
    , King
    ]


allSuits : List Suit
allSuits =
    [ Spade
    , Heart
    , Club
    , Diamond
    ]


newDeck : Deck
newDeck =
    List.foldl
        (\suit deck ->
            List.map (\face -> { face = face, suit = suit }) allFaces
                ++ deck
        )
        []
        allSuits


shuffleDeck : Deck -> Generator Deck
shuffleDeck =
    Random.List.shuffle


type Face
    = Ace
    | Two
    | Three
    | Four
    | Five
    | Six
    | Seven
    | Eight
    | Nine
    | Ten
    | Jack
    | Queen
    | King


type Suit
    = Spade
    | Heart
    | Club
    | Diamond


type alias Card =
    { face : Face
    , suit : Suit
    }


viewCard : Card -> Element msg
viewCard card =
    viewWrapper
        { content =
            case ( card.face, card.suit ) of
                -- Spades
                ( Ace, Spade ) ->
                    "ðŸ‚¡"

                ( Two, Spade ) ->
                    "ðŸ‚¢"

                ( Three, Spade ) ->
                    "ðŸ‚£"

                ( Four, Spade ) ->
                    "ðŸ‚¤"

                ( Five, Spade ) ->
                    "ðŸ‚¥"

                ( Six, Spade ) ->
                    "ðŸ‚¦"

                ( Seven, Spade ) ->
                    "ðŸ‚§"

                ( Eight, Spade ) ->
                    "ðŸ‚¨"

                ( Nine, Spade ) ->
                    "ðŸ‚©"

                ( Ten, Spade ) ->
                    "ðŸ‚ª"

                ( Jack, Spade ) ->
                    "ðŸ‚«"

                ( Queen, Spade ) ->
                    "ðŸ‚­"

                ( King, Spade ) ->
                    "ðŸ‚®"

                -- Hearts
                ( Ace, Heart ) ->
                    "ðŸ‚±"

                ( Two, Heart ) ->
                    "ðŸ‚²"

                ( Three, Heart ) ->
                    "ðŸ‚³"

                ( Four, Heart ) ->
                    "ðŸ‚´"

                ( Five, Heart ) ->
                    "ðŸ‚µ"

                ( Six, Heart ) ->
                    "ðŸ‚¶"

                ( Seven, Heart ) ->
                    "ðŸ‚·"

                ( Eight, Heart ) ->
                    "ðŸ‚¸"

                ( Nine, Heart ) ->
                    "ðŸ‚¹"

                ( Ten, Heart ) ->
                    "ðŸ‚º"

                ( Jack, Heart ) ->
                    "ðŸ‚»"

                ( Queen, Heart ) ->
                    "ðŸ‚½"

                ( King, Heart ) ->
                    "ðŸ‚¾"

                -- Clubs
                ( Ace, Club ) ->
                    "ðŸƒ‘"

                ( Two, Club ) ->
                    "ðŸƒ’"

                ( Three, Club ) ->
                    "ðŸƒ“"

                ( Four, Club ) ->
                    "ðŸƒ”"

                ( Five, Club ) ->
                    "ðŸƒ•"

                ( Six, Club ) ->
                    "ðŸƒ–"

                ( Seven, Club ) ->
                    "ðŸƒ—"

                ( Eight, Club ) ->
                    "ðŸƒ˜"

                ( Nine, Club ) ->
                    "ðŸƒ™"

                ( Ten, Club ) ->
                    "ðŸƒš"

                ( Jack, Club ) ->
                    "ðŸƒ›"

                ( Queen, Club ) ->
                    "ðŸƒ"

                ( King, Club ) ->
                    "ðŸƒž"

                -- Diamonds
                ( Ace, Diamond ) ->
                    "ðŸƒ"

                ( Two, Diamond ) ->
                    "ðŸƒ‚"

                ( Three, Diamond ) ->
                    "ðŸƒƒ"

                ( Four, Diamond ) ->
                    "ðŸƒ„"

                ( Five, Diamond ) ->
                    "ðŸƒ…"

                ( Six, Diamond ) ->
                    "ðŸƒ†"

                ( Seven, Diamond ) ->
                    "ðŸƒ‡"

                ( Eight, Diamond ) ->
                    "ðŸƒˆ"

                ( Nine, Diamond ) ->
                    "ðŸƒ‰"

                ( Ten, Diamond ) ->
                    "ðŸƒŠ"

                ( Jack, Diamond ) ->
                    "ðŸƒ‹"

                ( Queen, Diamond ) ->
                    "ðŸƒ"

                ( King, Diamond ) ->
                    "ðŸƒŽ"
        , color = getCardColor card
        , accessibleName = getCardName card
        }


getCardName : Card -> String
getCardName { suit, face } =
    getFaceName face ++ " of " ++ getSuitNamePlural suit


getFaceName : Face -> String
getFaceName face =
    case face of
        Ace ->
            "Ace"

        Two ->
            "Two"

        Three ->
            "Three"

        Four ->
            "Four"

        Five ->
            "Five"

        Six ->
            "Six"

        Seven ->
            "Seven"

        Eight ->
            "Eight"

        Nine ->
            "Nine"

        Ten ->
            "Ten"

        Jack ->
            "Jack"

        Queen ->
            "Queen"

        King ->
            "King"


getSuitNamePlural : Suit -> String
getSuitNamePlural suit =
    case suit of
        Spade ->
            "Spades"

        Club ->
            "Clubs"

        Heart ->
            "Hearts"

        Diamond ->
            "Diamonds"


getCardColor : Card -> Color
getCardColor { suit } =
    case suit of
        Spade ->
            Element.rgb 0 0 0

        Club ->
            Element.rgb 0 0 0

        Heart ->
            Element.rgb 1 0 0

        Diamond ->
            Element.rgb 1 0 0


viewCardBack : String -> Element msg
viewCardBack accessibleName =
    viewWrapper
        { color = Element.rgb 0.65 0 1
        , content = "ðŸ‚ "
        , accessibleName = accessibleName
        }


viewWrapper : { color : Color, content : String, accessibleName : String } -> Element msg
viewWrapper { color, content, accessibleName } =
    Element.el
        [ Element.Font.size 128
        , Element.Font.color color
        , Element.Background.color (Element.rgb 1 1 1)
        , Element.htmlAttribute (Html.Attributes.tabindex 0)
        , Element.Region.description accessibleName
        ]
        (Element.text content)


compareCards : Card -> Card -> Order
compareCards left right =
    case ( left.face, right.face ) of
        ( Ace, Ace ) ->
            EQ

        ( Ace, _ ) ->
            LT

        ( _, Ace ) ->
            GT

        ( Two, Two ) ->
            EQ

        ( Two, _ ) ->
            LT

        ( _, Two ) ->
            GT

        ( Three, Three ) ->
            EQ

        ( Three, _ ) ->
            LT

        ( _, Three ) ->
            GT

        ( Four, Four ) ->
            EQ

        ( Four, _ ) ->
            LT

        ( _, Four ) ->
            GT

        ( Five, Five ) ->
            EQ

        ( Five, _ ) ->
            LT

        ( _, Five ) ->
            GT

        ( Six, Six ) ->
            EQ

        ( Six, _ ) ->
            LT

        ( _, Six ) ->
            GT

        ( Seven, Seven ) ->
            EQ

        ( Seven, _ ) ->
            LT

        ( _, Seven ) ->
            GT

        ( Eight, Eight ) ->
            EQ

        ( Eight, _ ) ->
            LT

        ( _, Eight ) ->
            GT

        ( Nine, Nine ) ->
            EQ

        ( Nine, _ ) ->
            LT

        ( _, Nine ) ->
            GT

        ( Ten, Ten ) ->
            EQ

        ( Ten, _ ) ->
            LT

        ( _, Ten ) ->
            GT

        ( Jack, Jack ) ->
            EQ

        ( Jack, _ ) ->
            LT

        ( _, Jack ) ->
            GT

        ( Queen, Queen ) ->
            EQ

        ( Queen, _ ) ->
            LT

        ( _, Queen ) ->
            GT

        ( King, King ) ->
            EQ
